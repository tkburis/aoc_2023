func slice(str, i, j) {
    var ret = ""
    for (; i < j; i = i + 1) {
        ret = ret + str[i]
    }
    return ret
}

func split(str, delim) {
    var a = []
    var n = size(str)
    var start = 0

    var curr = 0
    for (; curr < n; curr = curr + 1) {
        if (curr + size(delim) <= n) {
            if (slice(str, curr, curr + size(delim)) == delim) {
                append(a, slice(str, start, curr))
                curr = curr + size(delim) - 1
                start = curr + 1
            }
        }
    }
    append(a, slice(str, start, curr))
    return a
}

var num_inputs = to_number(input(""))
var ans = 0
for (var num_input = 1; num_input <= num_inputs; num_input = num_input + 1) {
    if (num_input % 10 == 0) {print num_input}
    var input_string = input("")
    var subsets = split(split(input_string, ": ")[1], "; ")
    var maxes = {"red": 1, "green": 1, "blue": 1}
    for (var sub = 0; sub < size(subsets); sub = sub + 1) {
        var subset = split(subsets[sub], ", ")

        for (var i = 0; i < size(subset); i = i + 1) {
            var balls = split(subset[i], " ")
            if (to_number(balls[0]) > maxes[balls[1]]) {
                maxes[balls[1]] = to_number(balls[0])
            }
        }
    }
    ans = ans + maxes["red"] * maxes["green"] * maxes["blue"]
}
print(ans)
